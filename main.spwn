gs = import gamescene
extract obj_props

extract import "consts.spwn"
extract import "utils.spwn"
extract import "transitions.spwn"
extract import "graphics.spwn"
extract import "text.spwn"

lines = $.readfile("0.txt").split("\r\n")

let _BG_GROUPS = []

output = $.readfile("./output.txt", "json")
image = (file_name: @string, x_off: @number = 0, y_off: @number = 0) {
    if GENERATE_BACKGROUNDS == false { return }

    frame = output[file_name.split("/")[1:].join("")].reverse()
    _BG_GROUPS.push(?g)

    for j in 0..frame.length {
        for i in frame[j].length..0 {
            [h, s, v] = frame[j][i]

            $.add(obj{
                OBJ_ID: 211,
                X: x_off + i*10, Y: y_off + j*10 + (_BG_GROUPS.length-1)*1500,
                GROUPS: [_BACKGROUNDS_GROUP, _SCENE_GROUP, _BG_GROUPS[-1], _BACKGROUNDS_GROUP],
                Z_LAYER: -42,
                Z_ORDER: -2,
                SCALING: 0.335, // 1/10+0.01,
                COLOR: _RED_COLOR,
                HVS: @string(h)+"a"+@string(s/1000)+"a"+@string(v/1000+0.02)+"a0a0",
                HVS_ENABLED: true,
            })
        }
    }
    $.print(file_name, " added")
}


create_scene_wiper_thing(-440, 60)
// make bg invisible
$.add(obj{
    OBJ_ID: 1007, // alpha trigger
    X: -45, Y: 369,
    TARGET: _BACKGROUNDS_GROUP,
    OPACITY: 0,
    DURATION: 0,
})


// placeholder colors, remove this later
character_colors = ["195a0.8a0.96a0a0", "125a0.59a0.78a0a0", "304a0.53a0.95a0a0", "281a0.80a0.96a0a0"]

// generate and add characers
for i in 0..MAIN_CHARACTERS_KEYS.length {
    // placeholder
    $.add(obj{
        OBJ_ID: 211, // white block
        X: 80*3, Y: 150,
        GROUPS: [get_character(MAIN_CHARACTERS_KEYS[i]).group, _SCENE_GROUP],
        COLOR: _RED_COLOR,
        HVS: character_colors[i],
        HVS_ENABLED: true,
        SCALING: 4,
        Z_ORDER: -2,
        DONT_FADE: true,
        DONT_ENTER: true,
    })

    // alpha = 0 at the start to hide the characters
    $.add(obj{
        OBJ_ID: 1007, // alpha trigger
        X: -45, Y: 420,
        TARGET: get_character(MAIN_CHARACTERS_KEYS[i]).group,
        OPACITY: 0,
        DURATION: 0,
    })
}

// generate and add all bgs
for [ key, path ] in BACKGROUNDS {
    // jpeg(bg+".png", 42, 0)
    image(path, 42, 4)
}

for line in lines {
    if line.length == 0 { continue } // skip empty lines
    if line[0] == "#" { continue } // skip comments

    __tokens = generate_line_tokens(line)

    command = __tokens[0]
    if !(command in TOKENS) && command[0] != '"' { continue } // if its not a valid token/word
    tokens = __tokens[1:] // remove the command bit from tokens
    
    match command {
        in CHARACTERS_TOKENS: say_character(command, tokens, 300, 13, 169, 73),
        =="scene": set_scene(tokens, 300, 13),
        =="show": show_character(tokens, 300, 13),
        =="hide": hide_character(tokens),
        =="$": python_single_line(tokens),
        =="with": with(tokens, 300, 13),
    }
    
    // if narrator
    if command[0] == "\"" && tokens.length == 0 {
        say_narrator(command, 300, 13)
    }

    // $.print(modified_line)
}

$.add(obj{ // follow player
    OBJ_ID: 901, // move trigger id
    X: 225, Y: -135,
    TARGET: _SCENE_GROUP,
    MOVE_X: PLAYER_SPEED*30*LEVEL_DURATION, // move to player
    DURATION: LEVEL_DURATION,
})

$.add(obj{ // so the level is long enough or something
    OBJ_ID: 1,
    X: PLAYER_SPEED*30*LEVEL_DURATION, Y: -69,
})

$.add(obj{ // add invisible trigger at the start
    OBJ_ID: 1007, // alpha trigger id
    X: -31, Y: -69,
    TARGET: _INVISIBLE_GROUP,
    OPACITY: 0,
    DURATION: 0,
})

on(gs.button_a(), !{ // change through dialog boxes
    _TEXT_GROUP.move(0, -100, 0)
})

generate_dialogue_box()
generate_borders(302, 60)

print_words()
